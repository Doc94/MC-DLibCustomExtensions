plugins {
    id('java-library')
    id('maven-publish')
    id('signing')
    id('io.freefair.lombok') version "8.13.1"
    id('com.gradleup.shadow') version '8.3.6'
    id('org.jreleaser') version '1.18.0'
}

group = 'me.mrdoc.minecraft'
version = '1.0.0-SNAPSHOT'

ext {
    projectArtifactName = rootProject.name.toLowerCase()
    isRelease = !version.toString().endsWith('-SNAPSHOT')
    projectVersion = version.toString()
    projectGroup = group.toString()
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly(libs.paper)
    compileOnly(libs.configurate)
    compileOnly(libs.google.autoservice)

    implementation(libs.bundles.incendocloud)
    implementation(libs.romannumerals4j)

    annotationProcessor(libs.google.autoservice)
    annotationProcessor(libs.incendo.cloud.annotations)
}

java {
    withJavadocJar()
    withSourcesJar()

    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

shadowJar {
    relocate("org.incendo.cloud", "me.mrdoc.minecraft.dlce.libs.org.incendo.cloud")
    relocate("com.github.fracpet", "me.mrdoc.minecraft.dlce.libs.com.github.fracpet")
    archiveClassifier.set('')
}
build.dependsOn shadowJar

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.incremental = false
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked', '-Xdoclint:html,syntax,reference,accessibility']
}

tasks.withType(Javadoc).configureEach {
    title = "${projectArtifactName} ${projectVersion} API"
    options.windowTitle = "${projectArtifactName} ($projectVersion)"
}

tasks {
    compileJava {
        options.encoding = "UTF-8"
    }
    javadoc {
        options.encoding = "UTF-8"
    }
    jreleaserSign {
        dependsOn publish
    }
    jreleaserDeploy {
        dependsOn jreleaserSign
    }
}

tasks.register('downloadDependencies') {
    description 'Download all dependencies to the Gradle cache'
    doLast {
        configurations.findAll { it.canBeResolved }.files
    }
}

tasks.register("publishJar") {
    description 'Publish the JAR where need to go'

    dependsOn publish
    mustRunAfter publish

    if (isRelease) {
        dependsOn jreleaserSign, jreleaserDeploy
        mustRunAfter jreleaserSign
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "${projectGroup}"
            version = "${projectVersion}"
            artifactId = "${projectArtifactName}"

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = projectArtifactName
                description = 'A Minecraft library for custom things'
                url = 'https://mrdoc.me/'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
                developers {
                    developer {
                        id = 'doc'
                        name = 'Pedro'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Doc94/MC-DLibCustomExtensions.git'
                    developerConnection = 'scm:git:ssh://github.com/Doc94/MC-DLibCustomExtensions.git'
                    url = 'https://github.com/Doc94/MC-DLibCustomExtensions'
                }
            }
        }
    }

    repositories {
        maven {
            if (isRelease) {
                url = layout.buildDirectory.dir('staging-deploy/release')
            } else {
                credentials {
                    username = System.getenv('JRELEASER_MAVENCENTRAL_USERNAME')
                    password = System.getenv('JRELEASER_MAVENCENTRAL_PASSWORD')
                }
                url = 'https://central.sonatype.com/repository/maven-snapshots/'
            }
        }
    }
}

jreleaser {
    if (isRelease) {
        signing(({
            active = 'ALWAYS'
            armored = true
        } as Closure<Void>))
    }
    deploy {
        maven {
            mavenCentral(({
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    namespace = "${projectGroup}"
                    snapshotSupported = true
                    retryDelay = 30
                    if (isRelease) {
                        applyMavenCentralRules = true
                        stagingRepository('build/staging-deploy/release')
                    } else {
                        sign = false
                        stagingRepository('build/staging-deploy/snapshot')
                    }
                }
            } as Closure<Void>))
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
